Shader "ShaderDev/09VertAnimFlag"  {

    Properties {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex ("Main Texture", 2D) = "white" {}
        _Frequency ("Frequency", Float) = 1
        _Amplitude ("Amplitude", Float) = 1
        _Speed ("Speed", Float) = 1
    }

    SubShader {
        Tags { 
            "Queue" = "Transparent" 
            "IgnoreProjector" = "True" 
            "RenderType" = "Transparent"
        }

        Pass {
            Blend SrcAlpha OneMinusSrcAlpha

            CGPROGRAM
                #pragma vertex vert
                #pragma fragment frag

                uniform half4 _Color;
                uniform sampler2D _MainTex;
                uniform float4 _MainTex_ST; //variable generated by unity to tile and offset texture
                uniform float _Frequency;
                uniform float _Amplitude;
                uniform float _Speed;

                struct vertexInput {
                    float4 vertex : POSITION;
                    float4 texcoord : TEXCOORD0;
                };

                struct vertexOutput {
                    float4 pos : SV_POSITION;
                    float4 texcoord : TEXCOORD0;
                };

                float4 vertexFlagAnim(float4 vertPos, float2 uv) {
                    vertPos.z = vertPos.z + sin((uv.x - _Time.y * _Speed) * _Frequency) * (uv.x * _Amplitude);
                    return vertPos;
                }

                vertexOutput vert(vertexInput v) {
                    vertexOutput o;
                    v.vertex = vertexFlagAnim(v.vertex, v.texcoord);
                    UNITY_INITIALIZE_OUTPUT(vertexOutput, o); //necessary for HLSL compilers
                    //o.pos = mul(UNITY_MATRIX_MVP, v.vertex); //this method is obsolete and couls not support all graph API's
                    o.pos = UnityObjectToClipPos(v.vertex);
                    o.texcoord.xy = (v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw); //x and y coordinates are for tilling. z and w coordinates are for offseting
                    return o;
                }

                half4 frag(vertexOutput i) : COLOR {
                    float4 col = tex2D(_MainTex, i.texcoord) * _Color;
                    //col.a = drawCircleAnimate(i.texcoord, _Center.xy, _Radius, _Feather);
                    return col;
                }
            ENDCG
        }
    }

    FallBack "Diffuse"
}