Shader "ShaderDev/12Outline" {
    Properties {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex("Main Texture", 2D) = "white" {}
        _Outline ("Outline", Float) = 0.1
        _OutlineColor ("Outline Color", Color) = (1,1,1,1)
    }

    SubShader {
        Tags { 
            "Queue" = "Transparent" 
            "IgnoreProjector" = "True" 
            "RenderType" = "Transparent"
        }

        Pass {
            Blend SrcAlpha OneMinusSrcAlpha
            Cull Front
            Zwrite off
            CGPROGRAM
                #pragma vertex vert
                #pragma fragment frag

                uniform half _Outline;
                uniform half4 _OutlineColor;

                struct vertexInput {
                    float4 vertex : POSITION;
                };

                struct vertexOutput {
                    float4 pos : SV_POSITION;
                };

                float4 Outline(float4 vertPos, float outline) {
                    float4x4 scaleMat;
                    scaleMat[0][0] = 1.0 + outline;
                    scaleMat[0][1] = 0.0;
                    scaleMat[0][2] = 0.0;
                    scaleMat[0][3] = 0.0;
                    scaleMat[1][0] = 0.0;
                    scaleMat[1][1] = 1.0 + outline;
                    scaleMat[1][2] = 0.0;
                    scaleMat[1][3] = 0.0;
                    scaleMat[2][0] = 0.0;
                    scaleMat[2][1] = 0.0;
                    scaleMat[2][2] = 1.0 + outline;
                    scaleMat[2][3] = 0.0;
                    scaleMat[3][0] = 0.0;
                    scaleMat[3][1] = 0.0;
                    scaleMat[3][2] = 0.0;
                    scaleMat[3][3] = 1.0;
                    return mul(scaleMat, vertPos);
                }

                vertexOutput vert(vertexInput v) {
                    vertexOutput o;
                    UNITY_INITIALIZE_OUTPUT(vertexOutput, o); //necessary for HLSL compilers
                    float4 vertPos = Outline(v.vertex, _Outline);
                    //o.pos = mul(UNITY_MATRIX_MVP, Outline(v.vertex, _Outline)); //this method is obsolete and couls not support all graph API's
                    o.pos = UnityObjectToClipPos(Outline(v.vertex, _Outline));
                    return o;
                }

                half4 frag(vertexOutput i) : COLOR {
                    return _OutlineColor;
                }

            ENDCG
        }

        Pass {
            Blend SrcAlpha OneMinusSrcAlpha
            Zwrite on
            CGPROGRAM
                #pragma vertex vert
                #pragma fragment frag

                uniform half4 _Color;
                uniform sampler2D _MainTex;
                uniform float4 _MainTex_ST; //variable generated by unity to tile and offset texture

                struct vertexInput {
                    float4 vertex : POSITION;
                    float4 texcoord : TEXCOORD0;
                };

                struct vertexOutput {
                    float4 pos : SV_POSITION;
                    float4 texcoord : TEXCOORD0;
                };

                vertexOutput vert(vertexInput v) {
                    vertexOutput o;
                    UNITY_INITIALIZE_OUTPUT(vertexOutput, o); //necessary for HLSL compilers
                    //o.pos = mul(UNITY_MATRIX_MVP, v.vertex); //this method is obsolete and couls not support all graph API's
                    o.pos = UnityObjectToClipPos(v.vertex);
                    o.texcoord.xy = (v.texcoord.xy * _MainTex_ST.xy + _MainTex_ST.zw); //x and y coordinates are for tilling. z and w coordinates are for offseting
                    return o;
                }

                half4 frag(vertexOutput i) : COLOR {
                    return tex2D(_MainTex, i.texcoord) * _Color;
                }

            ENDCG
        }
    }

    FallBack "Diffuse"
}